plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.4'
  id 'org.owasp.dependencycheck' version '7.3.0'
  id 'org.sonarqube' version '3.4.0.2513'
  id 'com.github.ben-manes.versions' version '0.43.0'
  id 'io.freefair.lombok' version '6.5.1'
  id 'org.jetbrains.kotlin.jvm' version '1.7.20'
}

apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'org.sonarqube'

group = 'uk.gov.hmcts.reform.pip'
mainClassName = 'uk.gov.hmcts.reform.pip.subscription.management.Application'
version = '0.0.1'

allprojects {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.43.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacoco {
  toolVersion = "0.8.6"
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = []
sonarqube {
  properties {
    property "sonar.projectKey", "pip-subscription-management"
    property "sonar.exclusions", sonarExclusions.join(', ')
    property "sonar.dynamicAnalysis", "reuseReports"
    property "sonar.java.coveragePlugin", "jacoco"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

ext {
  reformLoggingVersion = "5.1.9"
}
dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.12'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLoggingVersion

  implementation group: 'com.azure.spring', name: 'spring-cloud-azure-starter-active-directory', version: '4.4.0'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.4.1'
  implementation group: 'com.github.hmcts', name: 'pip-data-models', version: '1.1.1', {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jpa'
  }
  implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.10.0'
  implementation group: 'com.vladmihalcea', name: 'hibernate-types-52', version: '2.14.0'

  // Force upgrade snakeyaml version for CVE-2022-38752
  implementation( group: 'org.yaml', name: 'snakeyaml').version {
    strictly("1.33")
  }

  // Force upgrade commons version for CVE-2022-42889
  implementation( group: 'org.apache.commons', name: 'commons-text').version {
    strictly("1.10.0")
  }

  testImplementation(platform('org.junit:junit-bom:5.9.1'))
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.63'
  implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.63'

  testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'

  testImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'
  testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: '2.7.10'
  testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '4.10.0'
  testImplementation group: 'org.springframework.security', name: 'spring-security-test'
  testImplementation "io.zonky.test:embedded-database-spring-test:2.1.2"

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation sourceSets.test.runtimeClasspath
}

bootJar {
  archiveFileName = "pip-subscription-management.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

//To use multiple application.yamls (for db mocking)
rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

ext['log4j2.version'] = '2.17.1'
